?rule_start:       rule |
?expression_start: top_expression |
rule:         non_terminal "=" top_expression
top_expression: expression+ | bare_choice
?expression: (non_terminal|wildcard |COMMA|PERIOD|choice|WORD)+ | "(" (non_terminal|wildcard|COMMA|PERIOD|choice|WORD)+ ")"
choice:       "(" expression+ ("|" expression+ )+  ")"
bare_choice:  expression+ ("|" expression+ )+

non_terminal: "$"CNAME
wildcard:     "{" _card_type meta? OBFUSCATED?"}"
meta: "meta:" (WORD | wildcard | COMMA | PERIOD | INT)+

_card_type: (location_wildcard_super | name_wildcard | object_wildcard_super | void_wildcard | pronoun_wildcard | question_wildcard | gesture_wildcard | category_wildcard | whattosay_wildcard)

?location_wildcard_super: loc_beacon_wildcard | loc_placement_wildcard | loc_room_wildcard | location_wildcard
location_wildcard:  "location"
loc_placement_wildcard: "location"? "placement" INT?
loc_beacon_wildcard:    "location"? "beacon" INT?
loc_room_wildcard:      "location"? "room" INT?

?object_wildcard_super: object_alike_wildcard | object_known_wildcard | object_wildcard
object_wildcard:            "object" INT?
object_known_wildcard:      "object known" | "kobject"
object_alike_wildcard:      "object alike" | "aobject"

name_wildcard:      "name" [WORD | INT] | "female" | "male"
question_wildcard:  "question"
pronoun_wildcard:   "pron" "pos"?
void_wildcard:      "void"
gesture_wildcard:   "gesture"
category_wildcard:   "category" INT?
whattosay_wildcard: "whattosay"

COMMA: ","
PERIOD: "."
OBFUSCATED:    "?"
WORD: (LETTER | "'")+
COMMENT: ("#"|";"|"//") /[^\n]/*

%ignore COMMENT
%import common.ESCAPED_STRING
%import common.INT
%import common.LETTER
%import common.WS
%import common.CNAME
%ignore WS